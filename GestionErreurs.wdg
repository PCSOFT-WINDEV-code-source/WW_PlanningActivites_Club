#To edit and compare internal_properties, use WINDEV integrated tools.
#Internal properties refer to the properties of controls in windows, reports, etc.
info :
 name : GestionErreurs
 major_version : 28
 minor_version : 0
 type : 7
 description : ""
 subtype : 0
procedure_set :
 identifier : 0x147493a00029d6b5
 internal_properties : CAAAAAgAAABGLu41kG7fjQV3iS4F72qmnKaNh5694reolNKIW0iw
 code_elements :
  type_code : 31
  p_codes :
   -
     code : |1-
      
      PRIVÉ
      // Gestion des erreurs
      gsTexteErreurSite		est une chaîne
      
      
      // Gestion des paramètres du site
      CONSTANT
      		
      	CN_HISTORIQUE_TYPEOPE_INFO		= 0
      	CN_HISTORIQUE_TYPEOPE_MODIF		= 1
      	CN_HISTORIQUE_TYPEOPE_ERREUR	= 2
      	CN_HISTORIQUE_TYPEOPE_APPEL_WS	= 3
      	
      fin
     type : 720896
  procedures :
   -
     name : AjouteErreur
     procedure_id : 1473965297758821300
     type_code : 15
     code : |1-
      // Résumé : Stocke et remonte une erreur
      // Syntaxe :
      //AjouteErreur (<sTexteErreur> est chaîne [, <sErreurTechnique> est chaîne [, <bEnvoieErreur> est booléen]])
      //
      // Paramètres :
      //	sTexteErreur (chaîne ANSI) : Texte de l'erreur
      //	sErreurTechnique (chaîne ANSI) : Détail technique de l'erreur
      //	bEnvoieErreur (booléen - valeur par défaut=1) : Envoie l'erreur au WebMaster si demandé
      // Valeur de retour :
      // 	Aucune
      //
      
      PROCEDURE AjouteErreur(sTexteErreur est une chaine, sErreurTechnique est une chaine = HErreurInfo(), bEnvoieErreur est un booléen = Vrai)
      
      // Pour le mode test, on remonte en assertion
      dbgAssertion(Faux,"Erreur détectée : "+sTexteErreur+RC+sErreurTechnique)
      
      // Est-ce qu'un détail technique est disponible
      SI sErreurTechnique = "" alors
      	gsTexteErreurSite = gsTexteErreurSite + [RC] + sTexteErreur
      SINON
      	gsTexteErreurSite = gsTexteErreurSite + [RC] + sTexteErreur + RC + sErreurTechnique
      FIN	
      
      // On note dans l'historique
      _AjoutHistoriqueSite(CN_HISTORIQUE_TYPEOPE_ERREUR,sTexteErreur+RC+sErreurTechnique+RC+dbgInfo(dbgPile),*,*,bEnvoieErreur)
     type : 458752
   -
     name : InitialiseErreur
     procedure_id : 1473965297758886930
     type_code : 15
     code : |1-
      // Résumé : Initialise le mécanisme de remontée des erreurs
      // Syntaxe :
      //InitialiseErreur ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Aucune
      //
      
      PROCEDURE InitialiseErreur()
      
      gsTexteErreurSite = ""
     type : 458752
   -
     name : _AjoutHistoriqueSite
     internal_properties : CAAAAAgAAAAeQeZWqR4YD9gNuw5ISpse4q5d4YfQh8vxSSLWonylqfGpzXAstIQfjtu5yNZJ9N/c91UXiZv53fcJCNn0zKQH2jXtD1BPimLproJX87E+1RbRiyhFUUvyPW0p6P5mipLcRRthNLb+/G4cHMU2fOcWaLmnLrlIMic+6HTcyeP/rQgO
     procedure_id : 1473973694420005211
     type_code : 15
     code : |1+
      // Résumé : Ajoute une information ou une erreur à l'historique du site
      // Syntaxe :
      //_AjoutHistoriqueSite (<nTypeOpération> est entier, <sDétailOpération> est chaîne [, <sValeurAvant> est chaîne [, <sValeurAprès> est chaîne [, <bEnvoieErreur> est booléen]]])
      //
      // Paramètres :
      //	nTypeOpération (entier) : type d'opération (voir constantes CN_HISTORIQUE_TYPEXXXX)
      //	sDétailOpération (chaîne ANSI) : Détail de l'opération ou de l'erreur
      //	sValeurAvant (chaîne ANSI - valeur par défaut="") : Valeur avant modification
      //	sValeurAprès (chaîne ANSI - valeur par défaut="") : Valeur après modification
      //	bEnvoieErreur (booléen - valeur par défaut=1) : Envoie l'erreur au WebMaster si demandé
      // Valeur de retour :
      // 	Aucune
      //
      //// Traitement automatique des exceptions :   exécuter le bloc de code CAS EXCEPTION:
      //
      
      PROCEDURE _AjoutHistoriqueSite(nTypeOpération est un entier, sDétailOpération est une chaine, sValeurAvant est une chaine="", sValeurAprès est une chaine ="", bEnvoieErreur est un booléen = Vrai)
      
      // Initialisation de l'historique
      HRAZ(Historique)
      
      // On note les informations automatiques
      Historique.DateHeure = DateSys()+HeureSys()
      Historique.typeUtilisation = CN_HISTORIQUE_TYPE_WEB
      // On prend l'adresse IP avec renvoi (cas par exemple d'un proxy) et l'adresse IP directe
      Historique.AdresseIP		= NavigateurAdresseIP(Vrai)+" / "+NavigateurAdresseIP(Faux)
      
      
      // On prend les valeurs fournies en paramètres
      Historique.ValeurAvant = sValeurAvant
      Historique.ValeurApres = sValeurAprès
      Historique.DetailOpération = sDétailOpération
      Historique.typeOpération = nTypeOpération
      
      // Ajout en base
      SI HAjoute(Historique) = Faux ALORS
      	dbgAssertion(Faux,"Impossible de sauver l'historique")
      	fSauveTexte(fRepDonnées()+["\"]+"dump_erreur_"+DateSys()+"_"+HeureSys()+".log","Erreur lors de la sauvegarde de l'historique : "+HErreurInfo())
      FIN
      
      SI nTypeOpération = CN_HISTORIQUE_TYPEOPE_ERREUR ALORS
      	// Notification en cas d'erreur
      	SI bEnvoieErreur = Vrai ALORS
      		//On peut gérer ici l'envoi d'un email au webmaster du site pour l'avertir d'une erreur sur le site
      	FIN
      FIN
      
      CAS EXCEPTION:
      dbgAssertion(Faux,"Impossible de sauver l'historique")
      fSauveTexte(fRepDonnées()+["\"]+"dump_erreur_"+DateSys()+"_"+HeureSys()+".log","Erreur lors de la sauvegarde de l'historique : "+HErreurInfo())
     type : 458752
  procedure_templates : []
  property_templates : []
 code_parameters :
  internal_properties : CAAAAAgAAACVGQgYbalU7DKC3oH4ItDrjtcTHXXV/EuH8q0IbhyQlw==
  original_name : COL_SansNom1
resources :
 string_res :
  identifier : 0x10f6dba505b82fbf
  internal_properties : CAAAAAgAAACm76HWfKGWp33VjXInA4cRlqArlgTTA862QGt72W2ld5Y=
custom_note :
 internal_properties : CAAAAAgAAABtB9HWVzrXO2+4NDRVK0vmzaNKrCKqH1DBX30lMmGZ
